
# Skript Made by JustGinoo
# CustomFishing (v1.1)

# ----- [Lootpool] ----- #

# You can change these items to create your own lootpool
on load:
    set {-FishingLoot::common::*} to cod, salmon, seagrass, kelp, sea pickle
    set {-FishingLoot::uncommon::*} to pufferfish, ink sac
    set {-FishingLoot::rare::*} to tropical fish, nautilus shell
    set {-FishingLoot::legendary::*} to enchanted book of mending, turtle scute



# ----- [Bite time] ----- #

on fish approach:
    set fishing bite time to (15 ticks) # Change this value if you like :)



# ----- [Fishing Event] ----- #

on fish catch:
    play sound "entity.chicken.egg" with pitch 0 to player

    chance of 2.5%:
        set {_item} to random element out of {-FishingLoot::legendary::*}
        play sound "entity.player.levelup" with pitch 1.5 to player
        make 25 flame at block above current fish hook of player with extra 0.075
        fishingAnimation(player, {_item}, 4)
    if {_item} is not set:
        chance of 10%:
            set {_item} to random element out of {-FishingLoot::rare::*}
            make 25 soul fire flame at block above current fish hook of player with extra 0.075
            fishingAnimation(player, {_item}, 3)
    if {_item} is not set:
        chance of 25%:
            set {_item} to random element out of {-FishingLoot::uncommon::*}
            make 10 sneeze at block above current fish hook of player with extra 0.075
            fishingAnimation(player, {_item}, 2)
    if {_item} is not set:
        chance of 100%:
            set {_item} to random element out of {-FishingLoot::common::*}
            make 10 cloud at block above current fish hook of player with extra 0.075
            fishingAnimation(player, {_item}, 1)

    set item of fishing caught entity to {_item}



# ----- [ItemName Display] ----- #

function fishingAnimation(p: player , item: item , c: int):

    set {_case} to random integer between 0 and 1000000
    spawn text display at block above block above current fish hook of {_p}
    set {_dt::%{_case}%} to last spawned text display
    set display text background color of {_dt::%{_case}%} to bukkitColor(0,255,255,255)
    set display default text background of {_dt::%{_case}%} to false
    set display billboard of {_dt::%{_case}%}  to vertical
    set display text of {_dt::%{_case}%} to "&7&l♦ &f%{_item}% &7&l♦" if {_c} = 1
    set display text of {_dt::%{_case}%} to "&2&l♦ &a%{_item}% &2&l♦" if {_c} = 2
    set display text of {_dt::%{_case}%} to "&3&l♦ &b%{_item}% &3&l♦" if {_c} = 3
    set display text of {_dt::%{_case}%} to "&6&l♦ &e%{_item}% &6&l♦" if {_c} = 4
    set {_loc} to location of {_dt::%{_case}%}
    add (random number between -0.5 and 0.5) to z-coord of {_loc}
    add (random number between -0.5 and 0.5) to x-coord of {_loc}
    remove 0.2 from y-coord of {_loc}
    set {_i} to 0
    set {_opacity} to 200
    while {_dt::%{_case}%} is alive:
        if {_i} <= 50:
            set display text opacity of {_dt::%{_case}%} to {_opacity} - 1
            add 1 to {_i}
            add 0.025 to y-coord of {_loc}
            teleport {_dt::%{_case}%} to {_loc}
            wait 1 tick
        else:
            kill {_dt::%{_case}%}
            wait 1 tick
